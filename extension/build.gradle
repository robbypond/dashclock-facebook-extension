apply plugin: 'android'
apply plugin: 'crashlytics'
apply plugin: 'android-test'

android {
    compileSdkVersion 19
    buildToolsVersion '19.0.3'

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    def Properties versionProps = new Properties()
    versionProps.load(new FileInputStream(file('version.properties')))

    defaultConfig {
        minSdkVersion 17
        targetSdkVersion 19
        versionName versionProps['name']
        versionCode versionProps['code'].toInteger()
    }
    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('../keystore.properties')))
            storeFile file(keyProps["store"])
            keyAlias keyProps["alias"]
            storePassword keyProps["storePass"]
            keyPassword keyProps["pass"]
        }
    }
    buildTypes {
        release {
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }
    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile 'com.crashlytics.android:crashlytics:1.+'
    compile 'com.google.android.apps.dashclock:dashclock-api:2.+'
    compile 'com.netflix.rxjava:rxjava-android:0.18.1'
    compile project(':facebook-android-sdk')
    compile project(':billing')

    androidTestCompile 'junit:junit:4.11'
    androidTestCompile 'org.robolectric:robolectric:2.1.+'
    androidTestCompile 'com.squareup:fest-android:1.0.+'
}
